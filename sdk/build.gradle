plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'signing'
}

def libraryVersion = '0.0.1'


android {
    namespace 'io.inappchat.sdk'
    compileSdk 33

    defaultConfig {
        minSdk 26
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        aarMetadata {
            minCompileSdk 33
        }
    }
    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.3"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', "ENV", '"production"'
            buildConfigField 'String', 'BASE_URL', '"https://prov.inappchat.io"'
        }
        debug {
            buildConfigField 'String', "ENV", '"dev"'
            buildConfigField 'String', 'BASE_URL', '"https://prov-dev.inappchat.io"'
        }
        qa {
            initWith debug
            buildConfigField 'String', "ENV", '"qa"'
            buildConfigField 'String', 'BASE_URL', '"https://prov-qa.inappchat.io"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        excludes += '/META-INF/INDEX.LIST'
    }
}

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation "com.squareup.moshi:moshi-kotlin:1.14.0"
    implementation "com.squareup.moshi:moshi-adapters:1.14.0"
    implementation 'androidx.appcompat:appcompat:1.6.1'


    implementation("com.hivemq:hivemq-mqtt-client:1.3.0")

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'


    // define a BOM and its version

    // define any required OkHttp artifacts without version
    implementation("com.squareup.okhttp3:okhttp:4.10.0")
    implementation('com.squareup.okhttp3:logging-interceptor:4.10.0')

    implementation 'com.github.mrmike:ok2curl:0.8.0'

    implementation platform('com.google.firebase:firebase-bom:31.2.3')
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.github.vestrel00.contacts-android:core:0.2.4'
    implementation 'com.github.vestrel00.contacts-android:async:0.2.4'

    implementation 'com.rollbar:rollbar-java:1.9.0'
    implementation 'com.rollbar:rollbar-android:1.9.0@aar'


    implementation 'androidx.compose.material3:material3:1.0.1'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.4.2'
    implementation "androidx.compose.ui:ui-tooling:1.4.2"
    implementation "androidx.compose.material:material:1.4.2"


    implementation 'androidx.activity:activity-compose:1.7.1'
    implementation 'androidx.core:core-ktx:+'
    implementation 'androidx.core:core-ktx:+'
    debugImplementation "androidx.customview:customview:1.2.0-alpha02"
    debugImplementation "androidx.customview:customview-poolingcontainer:1.0.0"

    debugImplementation 'androidx.compose.ui:ui-tooling:1.4.2'

    implementation 'androidx.media3:media3-exoplayer:1.0.1'
    implementation "androidx.media3:media3-ui:1.0.1"

    implementation 'com.google.accompanist:accompanist-permissions:0.29.2-rc'
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "com.yazantarifi:markdown-compose:1.0.2"


    implementation 'com.giphy.sdk:ui:2.3.1'

    implementation("io.coil-kt:coil-compose:2.2.2")
    implementation("io.coil-kt:coil-gif:2.2.2")

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.6.4'
    implementation 'me.jorgecastillo:androidcolorx:0.2.0'

    implementation 'net.datafaker:datafaker:1.8.0'
    implementation "nl.coffeeit.aroma:emojipicker:1.0.6"
    implementation 'com.google.android.gms:play-services-location:21.0.1'

    implementation 'com.auth0.android:auth0:2.+'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

}

openApiGenerate {
    generatorName = "kotlin"
    inputSpec = "$rootDir/openapi.yaml".toString()
    outputDir = "$rootDir/sdk".toString()
    configOptions = [
            "annotationLibrary"       : "swagger1",
            "apiPackage"              : "io.inappchat.sdk.api",
            "library"                 : "jvm-retrofit2",
            "useCoroutines"           : "true",
            "groupId"                 : "io.inappchat",
            "packageName"             : "io.inappchat.sdk",
            "omitGradleWrapper"       : "true",
            "omitGradlePluginVersions": "true"
    ]
}



afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId 'io.inappchat'
                artifactId 'chat-sdk'
                version libraryVersion

                // Two artifacts, the `aar` (or `jar`) and the sources
                from components.release

                // Mostly self-explanatory metadata
                pom {
                    name = 'InAppChat'
                    description = 'InAppChat Android Chat SDK'
                    url = 'https://github.com/ripbullnetworks/inappchat-android'
                    licenses {
                        license {
                            name = 'Stream License'
                            url = 'https://github.com/ripbullnetworks/inappchat-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                          id = 'lightchaser411'
                          name = 'Zaid Daghestani'
                          email = 'zaid.daghestani@gmail.com'
                        }
                        // Add all other devs here...
                    }

                    // Version control info - if you're using GitHub, follow the 
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/ripbullnetworks/inappchat-android.git'
                        developerConnection = 'scm:git:ssh://github.com/ripbullnetworks/inappchat-android.git'
                        url = 'https://github.com/ripbullnetworks/inappchat-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}